buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.1.0'
    }
}

// Manifest version information!
def versionMajor = 1
def versionMinor = 1
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

apply plugin: 'com.android.application'
apply plugin: 'hugo'

repositories {
    jcenter()
}

def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.genesys.gms.mobile.callback.demo.legacy"
        minSdkVersion 13
        targetSdkVersion 21
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
        }

        release {
            debuggable false
            minifyEnabled true
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }
    }
}

dependencies {
    compile 'com.android.support:support-v4:21.0.+'
    compile 'com.android.support:support-v13:21.0.+'
    compile 'com.android.support:appcompat-v7:21.0.+'
    compile 'com.google.android.gms:play-services-base:6.5.+'

    compile 'com.squareup.dagger:dagger:1.2.+'
    provided 'com.squareup.dagger:dagger-compiler:1.2.+'

    compile 'joda-time:joda-time:2.7'

    compile 'com.squareup.okhttp:okhttp:2.2.+'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.+'
    compile 'com.squareup.retrofit:retrofit:1.9.+'
    debugCompile 'com.squareup.retrofit:retrofit-mock:1.9.+'

    compile 'de.greenrobot:eventbus:2.4.0'

    compile 'org.slf4j:slf4j-api:1.7.7'

    compile('org.cometd.java:cometd-java-client:2.5.1') {
        exclude group: 'org.eclipse.jetty'
    }
    compile 'org.eclipse.jetty.aggregate:jetty-all:7.6.9.v20130131'

    compile('com.github.tony19:logback-android-classic:1.1.1-3') {
        exclude group: 'com.google.android', module: 'android'
    }

}
